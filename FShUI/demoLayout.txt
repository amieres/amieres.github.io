lytTarget2 PlugIn
: View ParseMsgs
: Doc  main 

editorDataSel Var ""

target    ViewJS "n => n.includes('main ')?n:'main Docs "" ""'" Snippets.curSnp_content

goPrev    Action AF.SetVar     "Snippets.snippets_sel"        Snippets.prevSel
goNext    Action AF.SetVar     "Snippets.snippets_sel"        Snippets.nextSel

SetTarget Action AF.SetVar     "lytTarget2.Layout"        target
SetMain2  Action AF.SetVar     "AppFramework.mainDocV"    "lytDemo.main2"
SetMain   Action AF.SetVar     "AppFramework.mainDocV"    "lytDemo.main"

editMode    Var "0"
editModeNot ViewJS "n => !n" editMode
ToggleMode Action AF.SetVar editMode editModeNot

applyColor ViewJS "(a,b) => a == b ? '' : 'btn btn-primary' " lytTarget2.Layout target

Trigger   Doc    AF.TrigAction Snippets.snippets_sel "lytDemo.SetTarget"

gotoMain  button "click=@{SetMain} ;width:16px" "<<"
gotoMain2 button "click=@{SetMain2};width:16px" ">>"

SearchFor   Doc AF.InputLabel "" "Search" Snippets.searchFor

buttons ul ""
: div "margin:3px"
:: button "click=@{Snippets.IndentOut};title=indent out"     "<--"
:: button "click=@{Snippets.IndentIn};title=indent in"       "--> "

File span "class=input-group;margin:5px;id=LoadSnippets" 
: div "class=input-group-btn"
:: label "class=btn btn-info" "Load File"
::: div ""
:::: input "class=form-control;type=file;display: none;change=@{Snippets.LoadSnippets}" 
:: label "class=btn btn-primary;click=@{Snippets.SaveSnippets}" "Save File"

list div "display: flex;flex-direction: column;height:100%;" gotoMain File
: div    "margin:5px" SearchFor 
: Docs buttons
: div "overflow:auto;width:100%" Snippets.snippets_list
: ul "margin:3px"
:: button "click=@{Snippets.AddSnippet}   ;title=Add New Snippet" "+"
:: button "margin-left:20px;click=@{Snippets.DeleteSnippet};title=Delete  Snippet" "x"

editor   Doc    Monaco.editor Snippets.curSnp_content     "fsharp"   "vs-dark" ""
editor2  Doc    Monaco.editor Snippets.curSnp_explanation "markdown" "vs-dark" ""
MarkDown0 ViewJS "t => marked(t                   )"      Snippets.curSnp_explanation
MarkDown1 ViewJS "t => marked(t.split('\\-\\-')[0])"      Snippets.curSnp_explanation
MarkDown2 ViewJS "t => marked(t.split('\\-\\-')[1])"      Snippets.curSnp_explanation

contentX div "display: flex;flex-direction: column;flex:1;margin:10px" Trigger
:  h2        ""              Snippets.curSnp_name
:  Doc       AF.HtmlDoc MarkDown1
:  div       "height:100%;class=relative;flex:2" editor
:  div       "color:red;font-weight:800;white-space:pre-wrap"  Snippets.parseOut
:  span      "width:100%"
:: button    "click=@{SetTarget};class=@{applyColor}"  "Try me! >>"
:  Doc       AF.HtmlDoc MarkDown2
:  span      "width:100%"
:: button    "click=@{goPrev};title=@{Snippets.prevTxt}"  "Prev."
:: button    "click=@{goNext};title=@{Snippets.nextTxt}"  "Next"

content div "display: flex;flex-direction: column;flex:1;margin:10px" Trigger
:  h2        ""              Snippets.curSnp_name
:  Doc       AF.HtmlDoc MarkDown0
:  span      "width:100%"
:: button    "click=@{goPrev};title=@{Snippets.prevTxt}"  "Prev."
:: button    "click=@{goNext};title=@{Snippets.nextTxt}"  "Next"

snippet div "display: flex;flex-direction: column;flex:1;margin:10px" Trigger content

RightX div "display: flex; flex-direction:column" lytTarget2.main

Right horizontal 0-35-100
:   div       "height:100%;display:flex;flex-direction: column" 
::  h4        "background:blue;color:white;margin:0px" "FShUI Editor:"
::  div       "class=relative;flex:2" editor
::  div       "color:red;font-weight:800;white-space:pre-wrap"  Snippets.parseOut
::  span      "width:100%"
::: button    "click=@{SetTarget};class=@{applyColor}"  "Try me! >>"
: div "display: flex; flex-direction:column" lytTarget2.main

editorCombo div "display:flex;flex-direction:column; height:100%"
:  Doc       AF.InputLabel "" "Name:" Snippets.curSnp_name
:  div       "class=relative;flex:1"      editor2

mainLower1 vertical 0-35-100
:  div "display:flex;flex-direction:column" 
:: div "max-height:50%" list 
:: Docs editorCombo
:          vertical 0-60-100 snippet  Right 
    
mainLower0 vertical 0-15-100
: div "display:block;overflow:auto;width:100%" Snippets.snippets_list
: vertical 0-60-100 snippet Right


main  horizontal 40
:  div "border-bottom-style: solid;border-bottom-width: 1px;border-bottom-color: lightgrey;max-height:100%"
:: h3  "margin:10px;click=   @{ToggleMode}" "FShUI"
:  AF.DocReference "mainLower@{editMode}"

split vertical 0-60-100
: Doc  Monaco.editor lytDemo.Layout     "fsharp"   "vs-dark" ""
: Docs lytDemo.listEdit